{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg\n",
    "Pkg.status()\n",
    "Pkg.resolve()\n",
    "Pkg.update()\n",
    "Pkg.instantiate()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using SeismicWaves\n",
    "using LinearAlgebra\n",
    "\n",
    "###################################################################\n",
    "using Logging\n",
    "using Plots, Plots.Measures\n",
    "\n",
    "\n",
    "error_logger = ConsoleLogger(stderr, Logging.Info)\n",
    "\n",
    "function gaussian_vel_1D(nx, c0, c0max, r, origin=(nx+1)/2)\n",
    "    sigma = r / 3\n",
    "    amp = c0max - c0\n",
    "    f(x) = amp * exp(-(0.5 * (x - origin)^2 / sigma^2))\n",
    "    return c0 .+ [f(x) for x in 1:nx]\n",
    "end\n",
    "\n",
    "function gaussian_vel_2D(nx, ny, c0, c0max, r, origin=[(nx+1)/2, (ny+1)/2])\n",
    "    sigma = r / 3\n",
    "    amp = c0max - c0\n",
    "    f(x,y) = amp * exp(-(0.5 * ((x - origin[1])^2 + (y - origin[2])^2) / sigma^2))\n",
    "    return c0 .+ [f(x,y) for x in 1:nx, y in 1:ny]\n",
    "end\n",
    "\n",
    "function gaussian_vel_3D(nx, ny, nz, c0, c0max, r, origin=[(nx+1)/2, (ny+1)/2, (nz+1)/2])\n",
    "    sigma = r / 3\n",
    "    amp = c0max - c0\n",
    "    f(x,y,z) = amp * exp(-(0.5 * ((x - origin[1])^2 + (y - origin[2])^2 + (z - origin[3])^2) / sigma^2))\n",
    "    return c0 .+ [f(x,y,z) for x in 1:nx, y in 1:ny, z in 1:nz]\n",
    "end\n",
    "\n",
    "function disc_vel_1D(nx, c0, c0max, r, origin=(nx+1)/2)\n",
    "    vel = c0 .* ones(nx)\n",
    "    for i in 1:nx\n",
    "        if norm(origin - i) <= r\n",
    "            vel[i] = c0max\n",
    "        end\n",
    "    end\n",
    "    return vel\n",
    "end\n",
    "\n",
    "function disc_vel_2D(nx, ny, c0, c0max, r, origin=[(nx+1)/2, (ny+1)/2])\n",
    "    vel = c0 .* ones(nx, ny)\n",
    "    for i in 1:nx\n",
    "        for j in 1:ny\n",
    "            if norm(origin .- [i, j]) <= r\n",
    "                vel[i,j] = c0max\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return vel\n",
    "end\n",
    "\n",
    "function disc_vel_3D(nx, ny, nz, c0, c0max, r, origin=[(nx+1)/2, (ny+1)/2, (nz+1)/2])\n",
    "    vel = c0 .* ones(nx, ny, nz)\n",
    "    for i in 1:nx\n",
    "        for j in 1:ny\n",
    "            for k in 1:nz\n",
    "                if norm(origin .- [i, j, k]) <= r\n",
    "                    vel[i,j,k] = c0max\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return vel\n",
    "end\n",
    "\n",
    "function plot_nice_heatmap(A; lx=size(A,1), ly=size(A,2), step=1, shift=0)\n",
    "    heatmap(0-shift:step:lx-shift, 0-shift:step:ly-shift, A';\n",
    "            aspect_ratio=:equal, margin=5mm, cmap=:jet1, yflip=true, xlims=(0-shift,lx-shift), ylims=(0-shift,ly-shift))\n",
    "\n",
    "end\n",
    "\n",
    "function plot_nice_heatmap_grad(A; lx=size(A,1), ly=size(A,2), step=1, shift=0)\n",
    "    vmax = maximum(abs.(A))\n",
    "    heatmap(0-shift:step:lx-shift, 0-shift:step:ly-shift, A';\n",
    "            aspect_ratio=:equal, margin=5mm, cmap=:RdBu, yflip=true, xlims=(0-shift,lx-shift), ylims=(0-shift,ly-shift),\n",
    "            clims=(-vmax, vmax))\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# time stuff\n",
    "nt = 1000\n",
    "c0 = 1000\n",
    "c0max = 1300\n",
    "r = 35\n",
    "dh = dx = dy = 5\n",
    "dt = dh / sqrt(2) / c0max\n",
    "halo = 20\n",
    "rcoef = 0.0001\n",
    "times = collect(Float64, range(0.0; step=dt, length=nt)) # seconds\n",
    "\n",
    "##========================================\n",
    "# create a velocity model\n",
    "nx = 201\n",
    "ny = 201\n",
    "lx = (nx-1) * dx\n",
    "ly = (ny-1) * dy\n",
    "matprop_const = VpAcousticCDMaterialProperty(c0 .* ones(nx, ny))\n",
    "# gaussian perturbed model\n",
    "matprop_gauss = VpAcousticCDMaterialProperty(gaussian_vel_2D(nx, ny, c0, c0max, r))\n",
    "\n",
    "display(plot_nice_heatmap(matprop_const.vp; lx=nx*dx, ly=ny*dy, step=dh))\n",
    "p = plot_nice_heatmap(matprop_gauss.vp; lx=nx*dx, ly=ny*dy, step=dh)\n",
    "display(p)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# shots definition\n",
    "nshots = 20\n",
    "shots = Vector{Shot{Float64}}()  #Pair{Sources, Receivers}}()\n",
    "# source time function\n",
    "f0 = 10\n",
    "t0 = 4 / f0\n",
    "\n",
    "for i in 1:nshots\n",
    "    # sources definition\n",
    "    nsrcs = nshots\n",
    "    distsrcs = 30\n",
    "    xsrcs = (lx/2) .- distsrcs .* ((nsrcs+1)/2 .- collect(1:nsrcs))\n",
    "    ysrc  = (halo + 10) * dy\n",
    "    possrcs = reshape([xsrcs[i], ysrc], 1, 2)\n",
    "    srcs = Sources(\n",
    "        possrcs,\n",
    "        repeat(rickersource1D.(times, 2/f0, f0), nt, 1),\n",
    "        f0\n",
    "    )\n",
    "\n",
    "    # receivers definition\n",
    "    nrecs = 20\n",
    "    distrecs = 30\n",
    "    xrecs = (lx/2) .- distrecs .* ((nrecs+1)/2 .- collect(1:nrecs))\n",
    "    yrec  = ly - (halo + 10) * dy\n",
    "    posrecs = hcat(xrecs, fill(yrec, nrecs))\n",
    "    recs = Receivers(\n",
    "        posrecs,\n",
    "        nt\n",
    "    )\n",
    "\n",
    "    # add pair as shot\n",
    "    push!(shots, Shot(; srcs=srcs, recs=recs)) # srcs => recs)\n",
    "\n",
    "    # display shot\n",
    "    p = plot_nice_heatmap(matprop_gauss.vp; lx=nx*dh, ly=ny*dh, step=dh)\n",
    "    # display rays\n",
    "    for irec in 1:nrecs\n",
    "        plot!([possrcs[1,1], posrecs[irec,1]], [possrcs[1,2], posrecs[irec,2]]; color=:black)\n",
    "    end\n",
    "    # display sources and receivers\n",
    "    scatter!(possrcs[:,1], possrcs[:,2]; markershape=:star4, markersize=5, markercolor=:white, legend=nothing)\n",
    "    scatter!(posrecs[:,1], posrecs[:,2]; markershape=:utriangle, markersize=4, markercolor=:white, legend=nothing)\n",
    "    # display CPML boundaries\n",
    "    plot!([halo*dx, lx-halo*dx], [halo*dy, halo*dy]; color=:white)\n",
    "    plot!([halo*dx, lx-halo*dx], [ly-halo*dy, ly-halo*dy]; color=:white)\n",
    "    plot!([halo*dx, halo*dx], [halo*dy, ly-halo*dy]; color=:white)\n",
    "    plot!([lx-halo*dx, lx-halo*dx], [halo*dy, ly-halo*dy]; color=:white)\n",
    "    plot!(; clims=(1000, 1300))\n",
    "    display(p)\n",
    "\n",
    "    savefig(\"shot$(i)_$(c0max).png\")\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Input parameters for acoustic simulation\n",
    "boundcond = CPMLBoundaryConditionParameters(; halo=halo, rcoef=rcoef, freeboundtop=false)\n",
    "params = InputParametersAcoustic(nt, dt, [nx, ny], [dh, dh], boundcond)\n",
    "\n",
    "# Wave simulation builder\n",
    "wavesim = build_wavesim(params; gradient=true, parall=:threads, check_freq=ceil(Int, sqrt(nt)))\n",
    "\n",
    "# compute forward gaussian\n",
    "with_logger(error_logger) do\n",
    "    swforward!(wavesim, matprop_gauss, shots)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# # Display seismograms for each shot\n",
    "# for i in 1:nshots\n",
    "#     p = plot(shots[i].recs.seismograms)\n",
    "#     display(p)\n",
    "# end\n",
    "# # Check that seismograms are specular\n",
    "# for i in 1:(nshots-1)÷2\n",
    "#     @assert shots[i].recs.seismograms ≈ reverse(shots[nshots+1-i].recs.seismograms, dims=2)\n",
    "# end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# new receivers with observed seismograms\n",
    "shots_obs = Vector{Shot{Float64}}()  #Pair{Sources, Receivers}}()\n",
    "for i in 1:nshots\n",
    "    # receivers definition\n",
    "    recs = Receivers(copy(shots[i].recs.positions), nt; observed=copy(shots[i].recs.seismograms), invcov=Diagonal(ones(nt)))\n",
    "    # add pair as shot\n",
    "    push!(shots_obs, Shot(; srcs=shots[i].srcs, recs=recs)) # srcs => recs)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# compute gradients and misfit\n",
    "gradient, misfit = with_logger(error_logger) do\n",
    "    swgradient!(wavesim, matprop_const, shots_obs; compute_misfit=true)\n",
    "end\n",
    "misfit"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "p = plot_nice_heatmap_grad(gradient; lx=nx*dx, ly=ny*dy, step=dh)\n",
    "corner = 250 ÷ dx\n",
    "plot!([corner*dx, lx-corner*dx], [corner*dy, corner*dy]; color=:black)\n",
    "plot!([corner*dx, lx-corner*dx], [ly-corner*dy, ly-corner*dy]; color=:black)\n",
    "plot!([corner*dx, corner*dx], [corner*dy, ly-corner*dy]; color=:black)\n",
    "plot!([lx-corner*dx, lx-corner*dx], [corner*dy, ly-corner*dy]; color=:black)\n",
    "display(p)\n",
    "\n",
    "p = plot_nice_heatmap_grad(gradient[corner:end-corner,corner:end-corner]; lx=(nx-corner*2)*dx, ly=(ny-corner*2)*dy, step=dh, shift=-dx*corner)\n",
    "display(p)\n",
    "\n",
    "savefig(\"gradient_$(c0max).png\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Optim\n",
    "\n",
    "error_logger = ConsoleLogger(stderr, Logging.Warn)\n",
    "\n",
    "function fg!(F,G,x)\n",
    "    matprop = VpAcousticCDMaterialProperty(x)\n",
    "    if G != nothing && F != nothing\n",
    "        # compute gradients and misfit\n",
    "        gradient, misfit = with_logger(error_logger) do\n",
    "            swgradient!(wavesim, matprop, shots_obs; compute_misfit=true)\n",
    "        end\n",
    "        G .= gradient\n",
    "        @show misfit\n",
    "        return misfit\n",
    "    elseif G != nothing\n",
    "        # compute only gradient\n",
    "        gradient = with_logger(error_logger) do\n",
    "            swgradient!(wavesim, matprop, shots_obs)\n",
    "        end\n",
    "        G .= gradient\n",
    "    elseif F != nothing\n",
    "        # compute only misfit\n",
    "        misfit = with_logger(error_logger) do\n",
    "            swmisfit!(wavesim, matprop, shots_obs)\n",
    "        end\n",
    "        @show misfit\n",
    "        return misfit\n",
    "    end\n",
    "  end\n",
    "\n",
    "res = optimize(Optim.only_fg!(fg!), matprop_const.vp, Optim.LBFGS())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "sol = Optim.minimizer(res)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "display(plot_nice_heatmap(sol; lx=nx*dx, ly=ny*dy, step=dh))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.5",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.5"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
